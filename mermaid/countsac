#!/bin/csh
#
# Counts SAC files for MERMAID data, as well as raw and reviewed .evt
# and .pdf files, and changepoint (.cp) files.
#
# Author: Joel D. Simon
# Contact: jdsimon@alumni.princeton.edu | joeldsimon@gmail.com
# Last modified by JDS: 05-Mar-2021, Darwin Kernel Version 18.7.0
# (previously modified by fjsimons-at-alum.mit.edu, 02/12/2019)

set MERMAID = $MERMAID
find $MERMAID/processed/ -maxdepth 3 -type f -name '*sac' > $MERMAID/all_sac

# Count only Princeton SAC files
set floatnum = (08 09 10 11 12 13 16 17 18 19 20 21 22 23 24 25)
set sac_count = 0
printf "\n                ...Tallying the SAC files...                \n"
printf "\n             All      DET       LAST DET      REQ       LAST REQ\n"

# OSX does not have the standard date formatting commands.  Must use a
# pseudo-switch statement.  Redirect any error to the /dev/null/ to
# suppress output to stdout and check if it failed. On a proper Linux
# machine this will display as '01-Jan-0001'; in Mac terminal it breaks.
date -d 00010101 +'%d-%b-%Y' >& /dev/null
if ( $? == 0 ) then
    set osx = 0
else
    set osx = 1
endif

foreach float ($floatnum[*])
    # This tallies the SAC files.
    grep -v "prelim" $MERMAID/all_sac | grep "P-$float" > $MERMAID/temp
    set total = `cat $MERMAID/temp | wc -l`
    set det =  `grep .DET. $MERMAID/temp  | wc -l`
    set req =  `grep .REQ. $MERMAID/temp  | wc -l`
    rm $MERMAID/temp

    @ total_count += $total
    @ det_count += $det
    @ req_count += $req

    # This finds the date of the last SAC file.
    set perfloat_det = `ls -1 $MERMAID/processed/*P-$float*/*/*.DET.*sac | tail -1`
    set lasttime_det = `basename $perfloat_det | awk '{print substr($0,1,8)}'`

    ls -1 $MERMAID/processed/*P-$float*/*/*.REQ.*sac >& /dev/null
    if ( $? == 0 ) then
	set perfloat_req = `ls -1 $MERMAID/processed/*P-$float*/*/*.REQ.*sac | tail -1`
	set lasttime_req = `basename $perfloat_req | awk '{print substr($0,1,8)}'`
    else

    	set lasttime_req = '19700101'  # Dummy date stamp in case of empty.

    endif

    if ( $osx == 0 ) then
        set lastdate_det = `date -d $lasttime_det +'%d-%b-%Y'`
        set lastdate_req = `date -d $lasttime_req +'%d-%b-%Y'`

    else
	set lastdate_det = `date -jf "%Y%m%d" $lasttime_det +"%d-%b-%Y"`
	set lastdate_req = `date -jf "%Y%m%d" $lasttime_req +"%d-%b-%Y"`

    endif
    printf "Float %s : %5i    %5i    %s    %5i    %s\n" $float $total $det $lastdate_det $req $lastdate_req

end
rm $MERMAID/all_sac

printf -- "----------------------------------------------------------------\n"
printf "   Total : %5i    %5i                   %5i\n\n" $total_count $det_count $req_count

# Count .evt files.
set rawevt_count = 0
set rawevt_count = `find $MERMAID/events/raw -iname '*.evt' | wc -l`
set double_rawevt_count = `echo "2*$rawevt_count" | bc`  # Should equal number of pdfs

set revevt_count = 0
set revevt_count = `find $MERMAID/events/reviewed -iname '*.evt' | wc -l`

set need_review = 0
set need_review = `echo "$rawevt_count - $revevt_count" | bc`

# Count raw .pdf files (should be twice the raw .evt count)
set rawpdf_count = 0
set rawpdf_count = `find $MERMAID/events/raw -iname '*.pdf' | wc -l`

# Count .cp files.
set cp_count = 0
set cp_count = `find $MERMAID/events/changepoints/ -iname '*.cp' | wc -l`

# Count number of SAC files that failed to be properly processed by cpsac2evt.m
set failed_count = 0
set failed_count = `grep .sac $MERMAID/events/raw/matchall_fail.txt | wc -l`

printf "Total raw  .evt : %5i\n" $rawevt_count
printf "Total fail .evt : %5i\n" $failed_count
printf "Total      .cp  : %5i\n" $cp_count
printf "Total raw  .pdf : %5i [2 * raw .evt = %4i]\n" $rawpdf_count $double_rawevt_count
printf "Total rev  .evt : %5i\n" $revevt_count
printf "Unreviewed .evt : %5i\n\n" $need_review
